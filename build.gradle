/*
 * Sasquatch
 */
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jetty'
apply plugin: 'groovy'

/* task to generate the gradle wrapper script */
task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

/* Running jetty 9 */
configurations {
    markLogicTasks
    jetty9
}


task runJetty9(type: JavaExec) {
    main = "org.eclipse.jetty.runner.Runner"
    args = [war.archivePath]
    classpath configurations.jetty9
}

task markLogicInit (dependsOn: 'classes', type: JavaExec) {
    main = 'com.marklogic.sasquatch.gradle.MarkLogicInit'
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty("adminUser")) {
        args(adminUser.split(":"))
    }
    else {
        args = ["admin", "admin"]
    }
}

task markLogicApplication (dependsOn: 'classes', type: JavaExec) {
    main = 'com.marklogic.sasquatch.gradle.MarkLogicApplication'
    classpath = sourceSets.main.runtimeClasspath
}


/* a test groovy script to implement a build task. see src/main/groovy... */
task loadGithub (dependsOn: 'classes', type: JavaExec ) {
    main = 'com.marklogic.sasquatch.gradle.GithubSlurp'
    classpath = sourceSets.main.runtimeClasspath
    args 'sasquatch-data/jsondata.json'
}

/* a test groovy script to implement a build task. see src/main/groovy... */
task loadTestGithub (dependsOn: 'classes', type: JavaExec ) {
    main = 'com.marklogic.sasquatch.gradle.GithubSlurp'
    classpath = sourceSets.main.runtimeClasspath
    args 'src/test/resources/github/githubSample.json'
}

/* The code repositories to consult for dependencies */
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://developer.marklogic.com/maven2"
    }
}

dependencies {
// TODO these are really for gradle/build process only
    jetty9 "org.eclipse.jetty:jetty-runner:9.1.2.v20140210"
    compile gradleApi()
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7'

//the following are app dependencies.
    compile localGroovy()
    compile 'org.springframework:spring-webmvc:4.0.2.RELEASE'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile 'junit:junit:4.11'
    compile('com.marklogic:client-api-java:3.0-SNAPSHOT') {
        exclude(group: 'org.slf4j')
    }
    compile('com.jayway.jsonpath:json-path:0.9.1') {
        exclude(group: 'org.slf4j')
    }
    testCompile('org.springframework:spring-test:4.0.1.RELEASE')
}


